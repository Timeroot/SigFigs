import Mathlib.Analysis.RCLike.Basic
import Mathlib.Analysis.Calculus.Deriv.Slope

import SigFigs.ForMathlib

open NNReal

/-! # First-Order Ball Arithmetic

A **first-order ball** models a real number with uncertainty using a center value and a width
(which can be thought of a variance of a random variable), with semantics to propagate them
through arithmetic operations by acting on the center(s) to produce a new center, and scaling
the width based on the derivative for nonlinear functions. In this sense, it can be viewed
as arithmetic on the tangent space of the real line.

Three key points to be aware of here:
 * If we interpret a value `xÂ±Ïƒ` as representing a normally distributed variable `ğ’©(x, Ïƒ)`,
   and pass it through a nonlinear function, the output does not necessarily reflect the
   mean or variance of the output distribution. Rather, the new "midpoint" is f(x).
 * For two-argument functions, this treats intputs as independent variables. This means
   that `(xÂ±a) + (yÂ±b) = (x+y) Â± âˆš(a^2 + b^2)`, for instance. In particular,
   `2 * (xÂ±a) = 2x Â± 2a` is not equal to `(xÂ±a) + (xÂ±a) = 2x Â± (âˆš2 * a)`. Similarly,
   `x^2 â‰  x*x`.
 * This model is slightly more flexible than just the tangent space calculation: we don't
   need the maps to be differentiable to have a well-defined image, we can make do with
   any local lipschitz constant `k`, which will scale the variance by `k^2`.

We store these as a midpoint and variance (as opposed to standard deviation), since this means
that arithmetic can be done without involving square roots.

This is a version of the [delta method](https://en.wikipedia.org/wiki/Delta_method) for
uncertainty propagation.

## References

 * "An Introduction to Error Analysis: The Study of Uncertainties in Physical Measurements" by
   John Richard Taylor. [Google Books](https://books.google.ca/books/about/An_Introduction_to_Error_Analysis.html?id=U0YoQQAACAAJ&source=kp_book_description&redir_esc=y)
-/

@[ext]
structure FOBall where
  mid : â„
  var : â„â‰¥0

namespace FOBall

instance : Zero FOBall :=
  âŸ¨âŸ¨0, 0âŸ©âŸ©

instance : One FOBall :=
  âŸ¨âŸ¨1, 0âŸ©âŸ©

instance : NatCast FOBall :=
  âŸ¨fun n â†¦ âŸ¨n, 0âŸ©âŸ©

instance : IntCast FOBall :=
  âŸ¨fun z â†¦ âŸ¨z, 0âŸ©âŸ©

instance : RatCast FOBall :=
  âŸ¨fun q â†¦ âŸ¨q, 0âŸ©âŸ©

/-- Addition of `FOBall`s adds "in quadrature", so variances add linearly. -/
instance : Add FOBall :=
  âŸ¨fun x y â†¦ âŸ¨x.mid + y.mid, x.var + y.varâŸ©âŸ©

instance : Neg FOBall :=
  âŸ¨fun x â†¦ âŸ¨-x.mid, x.varâŸ©âŸ©

instance : Sub FOBall :=
  âŸ¨fun x y â†¦ x + -yâŸ©

/-- For multiplication we define (conforming to the "first-order" philosophy) a new variance of
`(x * y).var = x.var * y.mid^2 + y.var * x.mid^2`, in other words that
`V[XY] = V[X] ğ”¼[Y]^2 + V[Y] ğ”¼[X]^2`. This is the convention in _Taylor, Error Analysis_.

There is at least one other reasonable quantity one might expect, which is
`V[XY] = V[X] ğ”¼[Y]^2 + V[Y] ğ”¼[X]^2 + V[X] V[Y]`, which is the correct variance for
a product of normally distributed variables.
-/
instance : Mul FOBall :=
  âŸ¨fun x y â†¦ âŸ¨x.mid * y.mid, x.var * â€–y.midâ€–â‚Š^2 + y.var * â€–x.midâ€–â‚Š^2âŸ©âŸ©

/-!
An aside: this definition of addition and multiplication leads to a non-distributive algebraic
structure. The midpoints agree - they just undergo standard real arithmetic! - but the variances
differ. To understand why, let's consider `(X * (Y + Z)).var` and `(X * Y + X * Z).var`.
```
V[X(Y+Z)] = (true when X vs. Y+Z independent, to first order in V's)
V[X] ğ”¼[Y+Z]^2 + V[Y+Z] ğ”¼[X]^2 = (linearity of ğ”¼)
V[X] (ğ”¼[Y] + ğ”¼[Z])^2 + V[Y+Z] ğ”¼[X]^2 = (true when Y vs. Z independent)
V[X] (ğ”¼[Y] + ğ”¼[Z])^2 + (V[Y] + V[Z]) ğ”¼[X]^2 = (algebra)
V[X] ğ”¼[Y]^2 + 2 V[X] ğ”¼[Y] ğ”¼[Z] + V[X] ğ”¼[Z]^2 + V[Y] ğ”¼[X]^2 + V[Z] ğ”¼[X]^2
â‰ 
V[X] ğ”¼[Y]^2 +                    V[X] ğ”¼[Z]^2 + V[Y] ğ”¼[X]^2 + V[Y] ğ”¼[Z]^2 = (algebra)
(V[X] ğ”¼[Y]^2 + V[Y] ğ”¼[X]^2) + (V[X] ğ”¼[Z]^2 + V[Y] ğ”¼[Z]^2) = (true when X vs. Z independent, to first order in V's)
(V[X] ğ”¼[Y]^2 + V[Y] ğ”¼[X]^2) + V[XZ] = (true when X vs. Y independent, to first order in V's)
V[XY] + V[XZ] = (true when XY vs. XZ independent)
V[XY + XZ]
```
By treating the errors in `XY` and `XZ` as independent, instead of appropriately correlated,
we underestimate the variance.
-/

noncomputable instance : Inv FOBall :=
  âŸ¨fun x â†¦ âŸ¨x.midâ»Â¹, x.var / â€–x.midâ€–â‚Š^2âŸ©âŸ©

noncomputable instance : Div FOBall :=
  âŸ¨fun x y â†¦ x * yâ»Â¹âŸ©

instance instPowNat : Pow FOBall â„• where
  pow x n := âŸ¨x.mid ^ n, if n = 0 then 0 else x.var * â€–x.midâ€–â‚Š ^ (2 * (n - 1))âŸ©

instance instSMul : SMul â„• FOBall where
  smul n x := âŸ¨n â€¢ x.mid, n^2 â€¢ x.varâŸ©

theorem zero_def : (0 : FOBall) = âŸ¨0, 0âŸ© :=
  rfl

theorem natCast_def (n : â„•) : (n : FOBall) = âŸ¨n, 0âŸ© :=
  rfl

theorem intCast_def (z : â„¤) : (z : FOBall) = âŸ¨z, 0âŸ© :=
  rfl

theorem ratCast_def (q : â„š) : (q : FOBall) = âŸ¨q, 0âŸ© :=
  rfl

theorem one_def : (0 : FOBall) = âŸ¨0, 0âŸ© :=
  rfl

theorem add_def (x y : FOBall) : x + y = âŸ¨x.mid + y.mid, x.var + y.varâŸ© :=
  rfl

theorem neg_def (x : FOBall) : -x = âŸ¨-x.mid, x.varâŸ© :=
  rfl

theorem sub_def (x y : FOBall) : x - y = âŸ¨x.mid - y.mid, x.var + y.varâŸ© :=
  rfl

theorem mul_def (x y : FOBall) : x * y =
    âŸ¨x.mid * y.mid, x.var * â€–y.midâ€–â‚Š^2 + y.var * â€–x.midâ€–â‚Š^2âŸ© :=
  rfl

theorem inv_def (x : FOBall) : xâ»Â¹ = âŸ¨x.midâ»Â¹, x.var / â€–x.midâ€–â‚Š^2âŸ© :=
  rfl

theorem div_def (x y : FOBall) : x / y = âŸ¨x.mid / y.mid,
    x.var / â€–y.midâ€–â‚Š^2 + (y.var / â€–y.midâ€–â‚Š^2) * â€–x.midâ€–â‚Š^2âŸ© := by
  suffices x.var / â€–y.midâ€–â‚Š^2 = x.var * â€–y.midâ»Â¹â€–â‚Š^2 by
    rw [this]; rfl
  field_simp

@[simp]
theorem mid_zero : mid 0 = 0 := by
  rfl

@[simp]
theorem var_zero : var 0 = 0 := by
  rfl

@[simp]
theorem mid_one : mid 1 = 1 := by
  rfl

@[simp]
theorem var_one : var 1 = 0 := by
  rfl

@[simp]
theorem mid_ofNat (n : â„•) [n.AtLeastTwo] :
    (ofNat(n) : FOBall).mid = n := by
  rfl

@[simp]
theorem var_ofNat (n : â„•) [n.AtLeastTwo] :
    (ofNat(n) : FOBall).var = 0 := by
  rfl

--TODO: simps for IntCast / RatCast

@[simp]
theorem mid_add (x y : FOBall) : (x + y).mid = x.mid + y.mid := by
  rfl

@[simp]
theorem var_add (x y : FOBall) : (x + y).var = x.var + y.var := by
  rfl

@[simp]
theorem mid_neg (x : FOBall) : (-x).mid = -x.mid := by
  rfl

@[simp]
theorem var_neg (x : FOBall) : (-x).var = x.var := by
  rfl

@[simp]
theorem mid_sub (x y : FOBall) : (x - y).mid = x.mid - y.mid := by
  rfl

@[simp]
theorem var_sub (x y : FOBall) : (x - y).var = x.var + y.var := by
  rfl

@[simp]
theorem mid_mul (x y : FOBall) : (x * y).mid = x.mid * y.mid := by
  rfl

@[simp]
theorem var_mul (x y : FOBall) : (x * y).var =
    x.var * â€–y.midâ€–â‚Š^2 + y.var * â€–x.midâ€–â‚Š^2 := by
  rfl

theorem var_mul_toReal (x y : FOBall) : ((x * y).var : â„) =
    x.var * y.mid^2 + y.var * x.mid^2 := by
  simp

@[simp]
theorem mid_inv (x : FOBall) : (xâ»Â¹).mid = x.midâ»Â¹ := by
  rfl

@[simp]
theorem var_inv (x : FOBall) : (xâ»Â¹).var = x.var / â€–x.midâ€–â‚Š^2 := by
  rfl

theorem var_inv_toReal (x : FOBall) : ((xâ»Â¹).var : â„) = x.var / x.mid^2 := by
  simp

@[simp]
theorem mid_div (x y : FOBall) : (x / y).mid = x.mid / y.mid := by
  rfl

@[simp]
theorem var_div (x y : FOBall) : (x / y).var =
    x.var / â€–y.midâ€–â‚Š^2 + y.var * â€–x.mid / y.midâ€–â‚Š^2 := by
  field_simp [div_def]

theorem var_div_toReal (x y : FOBall) :  ((x / y).var : â„) =
    x.var / y.mid^2 + y.var * (x.mid / y.mid)^2 := by
  field_simp

/-!
We're so close to having a proper CommSemiring, but alas we miss distributivity.
We're not an additive group, because `a + (-a) â‰  0` whenever `a.var â‰  0`.
We're not a GroupWithZero, because `a * aâ»Â¹ â‰  1` whenever `a.var â‰  0`.
We're not a MulDivCancelClass, because `1 * (0Â±1) / (0Â±1)â»Â¹ = 0 â‰  1`.
We're not an InvolutiveInv, because `(0Â±1)â»Â¹â»Â¹ = (0Â±0)â»Â¹ = 0 â‰  0Â±1.
We're not a CancelCommMonoid, because `(0Â±1) * Â·` is not injective.
We're not a NoZeroDivisors, because `(0Â±1) * (0Â±1) = 0`, but if we included the "second order"
 term in multiplication, we would be.
-/

instance : AddCancelCommMonoid FOBall where
  add_assoc := by intros; simp [add_def, add_assoc]
  zero_add := by simp [FOBall.ext_iff]
  add_zero := by simp [FOBall.ext_iff]
  add_comm := by intros; simp [add_def, add_comm]
  add_left_cancel := by
    intro _ _ _ h
    simpa [FOBall.ext_iff] using h
  /- TODO: How do we hide this instance? We have it "by default" as soon as we have
    a AddMonoid (and we do), but it's not the semantics that we want. We want our
    instSMul to be higher priority when writing something down. We'll still have
    moderately crappy cases where (due to some rewrites) we end up with a `2 â€¢ Â·`
    expression using the wrong instance, but that might just be what we have to live
    with.
  -/
  nsmul := nsmulRec

instance : AddCommMonoidWithOne FOBall where
  natCast_zero := by simp [zero_def, natCast_def]
  natCast_succ := by simp [add_def, natCast_def]

instance : SubtractionCommMonoid FOBall where
  neg_neg _ := by simp [FOBall.ext_iff]
  neg_add_rev _ _ := by simp [FOBall.ext_iff, add_comm]
  neg_eq_of_add := by
    rintro âŸ¨mâ‚, vâ‚âŸ© âŸ¨mâ‚‚, vâ‚‚âŸ© h
    simp [FOBall.ext_iff] at h
    rcases h with âŸ¨h, rfl, rflâŸ©
    simp [FOBall.ext_iff, neg_eq_of_add_eq_zero_right h]
  --TODO hide this instance, see above
  zsmul := zsmulRec

instance : SubNegZeroMonoid FOBall where
  neg_zero := by simp

instance : CommMonoid FOBall where
  one_mul _ := by simp [FOBall.ext_iff]
  mul_one _ := by simp [FOBall.ext_iff]
  mul_assoc _ _ _ := by
    simp [mul_def, mul_pow]
    use by ring, by ring
  mul_comm _ _ := by
    simpa [mul_def] using And.intro (mul_comm _ _) (add_comm _ _)
  --TODO hide this instance, see above
  npow := npowRec

noncomputable instance : DivInvOneMonoid FOBall where
  inv_one := by simp [FOBall.ext_iff]
  --TODO hide this instance, see above
  zpow := zpowRec

noncomputable instance : MulZeroOneClass FOBall where
  zero_mul _ := by simp [FOBall.ext_iff]
  mul_zero _ := by simp [FOBall.ext_iff]

instance : HasDistribNeg FOBall where
  neg_mul _ _ := by simp [FOBall.ext_iff]
  mul_neg _ _ := by simp [FOBall.ext_iff]

noncomputable instance : MinimalRing FOBall where

/-- The ring homomorphism from â„ into FOBall (specifically, an isomorphism on the 0-width set.) -/
def pure : MinimalRingHom â„ FOBall where
  toFun x := âŸ¨x, 0âŸ©
  map_one' := rfl
  map_zero' := rfl
  map_mul' := by simp [FOBall.ext_iff]
  map_add' := by simp [FOBall.ext_iff]

instance : Coe â„ FOBall :=
  âŸ¨pureâŸ©

@[simp]
theorem mid_pure (x : â„) : (pure x).mid = x := by
  rfl

@[simp]
theorem var_pure (x : â„) : (pure x).var = 0 := by
  rfl

theorem pure_zero : pure 0 = 0 := by
  simp

theorem pure_one : pure 1 = 1 := by
  simp

@[simp]
theorem pure_natCast (n : â„•) : pure n = n := by
  rfl

@[simp]
theorem pure_intCast (z : â„¤) : pure z = z := by
  rfl

@[simp]
theorem pure_ratCast (q : â„š) : pure q = q := by
  rfl

theorem pure_add (x y : â„) : pure (x + y) = pure x + pure y := by
  simp only [map_add]

theorem pure_neg (x : â„) : pure (-x) = -pure x := by
  simp

theorem pure_sub (x y : â„) : pure (x - y) = pure x - pure y := by
  simp only [map_sub]

theorem pure_mul (x y : â„) : pure (x * y) = pure x * pure y := by
  simp only [map_mul]

@[simp]
theorem pure_inv (x : â„) : pure (xâ»Â¹) = (pure x)â»Â¹ := by
  ext <;> simp

@[simp]
theorem pure_div (x y : â„) : pure (x / y) = pure x / pure y := by
  ext <;> simp

section ofScientific

def ofScientific (m : â„•) (sign : Bool) (e : â„•) : FOBall :=
  let e' := (if sign then -e else e : â„¤);
  âŸ¨(m * 10^e' : â„š), âŸ¨(10^e' / 2 : â„š)^2, sq_nonneg _âŸ©âŸ©

instance : OfScientific FOBall where
  ofScientific := ofScientific

@[simp]
theorem ofScientific_mid (m : â„•) (sign : Bool) (e : â„•) :
    (OfScientific.ofScientific m sign e : FOBall).mid =
      (m * 10^(if sign then -e else e : â„¤) : â„š) := by
  rfl

@[simp]
theorem ofScientific_e (m : â„•) (sign : Bool) (e : â„•) :
    (OfScientific.ofScientific m sign e : FOBall).var =
      âŸ¨(10^(if sign then -e else e : â„¤) / 2 : â„š)^2, sq_nonneg _âŸ© := by
  rfl

end ofScientific

/-- The `a Â± b` notation parses as `p` with `p.mid = a` and `p.var = b^2`. -/
scoped macro n:term "Â±" pm:term : term => `((âŸ¨$n, âŸ¨$pm ^ 2, sq_nonneg $pmâŸ©âŸ© : FOBall))

section setlike

/-- Interpreting an `FOBall` as a confidence interval, is the given value
`x` consistent with it? -/
def mem (r : FOBall) (x : â„) : Prop :=
  (x - r.mid)^2 â‰¤ r.var

instance : SetLike FOBall â„ where
  coe r := setOf r.mem
  coe_injective' r s h := by
    simp [Set.ext_iff, mem] at h
    rcases lt_trichotomy (r.mid + âˆšr.var) (s.mid + âˆšs.var) with hâ‚‚ | hâ‚‚ | hâ‚‚
    rotate_left
    Â· rcases lt_trichotomy (r.mid - âˆšr.var) (s.mid - âˆšs.var) with hâ‚‚ | hâ‚‚ | hâ‚‚
      rotate_left
      Â· have hm : r.mid = s.mid := by linarith
        have hv : âˆšr.var = âˆšs.var := by linarith
        rw [Real.sqrt_inj (by positivity) (by positivity), â† NNReal.eq_iff] at hv
        simp [hm, hv, FOBall.ext_iff]
      Â· exfalso
        specialize h (s.mid - âˆšs.var)
        simp only [sub_sub_cancel_left, even_two, Even.neg_pow, zero_le_coe,
          Real.sq_sqrt, le_refl, iff_true] at h
        rw [â† one_mul (_^2), â† neg_one_sq, â† mul_pow, neg_mul] at h
        rw [â† Real.le_sqrt' (by linarith [Real.sqrt_nonneg r.var])] at h
        linarith
      Â· exfalso
        specialize h (r.mid - âˆšr.var)
        simp only [sub_sub_cancel_left, even_two, Even.neg_pow, zero_le_coe,
          Real.sq_sqrt, le_refl, true_iff] at h
        rw [â† one_mul (_^2), â† neg_one_sq, â† mul_pow, neg_mul] at h
        rw [â† Real.le_sqrt' (by linarith [Real.sqrt_nonneg r.var])] at h
        linarith
    Â· exfalso
      specialize h (r.mid + âˆšr.var)
      simp at h
      rw [â† Real.le_sqrt' (by linarith [Real.sqrt_nonneg s.var])] at h
      linarith
    Â· exfalso
      specialize h (s.mid + âˆšs.var)
      simp at h
      rw [â† Real.le_sqrt' (by linarith [Real.sqrt_nonneg r.var])] at h
      linarith

theorem mem_def (r : FOBall) (x : â„) : x âˆˆ r â†” (x - r.mid)^2 â‰¤ r.var := by
  rfl

section map

--For Mathlib
section lipschitzAt
open Topology

def LipschitzAtFilter {X Y : Type*} [EDist X] [EDist Y]
    (f : X â†’ Y) (l : Filter (X Ã— X)) : Prop :=
  âˆƒ (C : NNReal), âˆ€á¶  z in l, edist (f z.1) (f z.2) â‰¤ C * edist z.1 z.2

def LipschitzWithAtFilter {X Y : Type*} [EDist X] [EDist Y]
    (C : NNReal) (f : X â†’ Y) (l : Filter (X Ã— X)) : Prop :=
  âˆ€á¶  z in l, edist (f z.1) (f z.2) â‰¤ C * edist z.1 z.2

def LipschitzAt {X Y : Type*} [TopologicalSpace X] [EDist X] [EDist Y]
    (f : X â†’ Y) (p : X) : Prop :=
  âˆƒ (C : NNReal), âˆ€á¶  z in ğ“ p , edist (f z) (f p) â‰¤ C * edist z p

def LipschitzWithAt {X Y : Type*} [TopologicalSpace X] [EDist X] [EDist Y]
    (C : NNReal) (f : X â†’ Y) (p : X) : Prop :=
  âˆ€á¶  z in ğ“ p , edist (f z) (f p) â‰¤ C * edist z p

/-- The infimum of constants C so that `f` is C-Lipschitz on the filter `l`. -/
noncomputable def lipschitzAt {X Y : Type*} [TopologicalSpace X] [EDist X] [EDist Y]
    (f : X â†’ Y) (p : X) : NNReal :=
  sInf {C | LipschitzWithAt C f p}

theorem LipschitzWithAt.lipschitzAt {X Y : Type*} [TopologicalSpace X] [EDist X] [EDist Y]
    {C : NNReal} {f : X â†’ Y} {p : X} (hf : LipschitzWithAt C f p) : LipschitzAt f p :=
  âŸ¨C, hfâŸ©

@[simp]
theorem _root_.DifferentiableAt.LipschitzAt {f : â„ â†’ â„} {x : â„} (hf : DifferentiableAt â„ f x) :
    LipschitzAt f x := by
  obtain âŸ¨w, hâŸ© : âˆƒ C : â„, âˆ€á¶  z in ğ“ x, |f z - f x| â‰¤ C * |z - x| := by
    -- Since f is differentiable at x, we have lim_{z â†’ x} (f(z) - f(x)) / (z - x) = f'(x).
    have h_deriv : (ğ“[â‰ ] x).Tendsto (fun z => (f z - f x) / (z - x)) (ğ“ (deriv f x)) := by
      simpa [hasDerivAt_iff_tendsto_slope, div_eq_inv_mul] using hf.hasDerivAt
    obtain âŸ¨C, hCâŸ© : âˆƒ C : â„, âˆ€á¶  z in (ğ“[â‰ ] x), |(f z - f x) / (z - x)| â‰¤ C :=
      âŸ¨_, h_deriv.abs.eventually (ge_mem_nhds <| lt_add_one _)âŸ©
    -- Since |(f z - f x) / (z - x)| â‰¤ C for z â‰  x, multiplying both sides by |z - x| gives
    -- |f z - f x| â‰¤ C * |z - x| for z â‰  x. For z = x, |f z - f x| = 0 â‰¤ C * |z - x| trivially.
    use C
    have hl : âˆ€á¶  z in ğ“ x, z â‰  x â†’ |f z - f x| â‰¤ C * |z - x| := by
      rw [eventually_nhdsWithin_iff] at hC
      filter_upwards [hC] with z hz hzx
      simpa only [abs_div, div_le_iffâ‚€ (abs_pos.mpr (sub_ne_zero.mpr hzx))] using hz hzx
    filter_upwards [hl] with z hz using if h : z = x then by simp [h] else hz h
  use âŸ¨|w|, by positivityâŸ©
  filter_upwards [h] with z hz
  apply ENNReal.coe_le_coe.mpr
  simpa [abs_mul] using hz.trans (mul_le_mul_of_nonneg_right (le_abs_self _) (abs_nonneg _))

/-- For differentiable functions, the lipschitz constant at a point is the absolute
value of the derivative. -/
@[simp]
theorem _root_.DifferentiableAt.lipschitzAt_eq_deriv {f : â„ â†’ â„} {x : â„} (hf : DifferentiableAt â„ f x) :
    lipschitzAt f x = â€–deriv f xâ€–â‚Š := by
  simp only [NNReal.eq_iff, coe_nnnorm, Real.norm_eq_abs]
  have h_C_lipschitz : âˆ€ (C : NNReal), C > |deriv f x| â†’ LipschitzWithAt C f x := by
    have h_deriv : (ğ“[â‰ ] x).Tendsto (fun z â†¦ (f z - f x) / (z - x)) (ğ“ (deriv f x)) := by
      simpa [hasDerivAt_iff_tendsto_slope, div_eq_inv_mul] using hf.hasDerivAt
    intro C hC
    have h_bound : âˆ€á¶  z in ğ“[â‰ ] x, |(f z - f x) / (z - x)| â‰¤ C :=
      h_deriv.abs.eventually (ge_mem_nhds hC)
    rw [eventually_nhdsWithin_iff] at h_bound
    filter_upwards [h_bound] with y hy
    by_cases h : y = x
    Â· simp [h]
    simp [edist_dist, abs_div] at hy âŠ¢
    rw [div_le_iffâ‚€ (abs_pos.mpr (sub_ne_zero.mpr h))] at hy
    rw [â† ENNReal.ofReal_coe_nnreal, â† ENNReal.ofReal_mul (by positivity),
      ENNReal.ofReal_le_ofReal_iff (by positivity)]
    exact hy h
  apply le_antisymm
  Â· by_contra h_contra
    obtain âŸ¨C, hCâŸ© : âˆƒ C : â„â‰¥0, |deriv f x| < C âˆ§ C < lipschitzAt f x :=
      âŸ¨âŸ¨(|deriv f x| + lipschitzAt f x) / 2, by positivityâŸ©,
        by norm_num; linarith,
        NNReal.coe_lt_coe.mp (by norm_num; linarith)âŸ©
    exact hC.2.not_ge <| csInf_le âŸ¨0, fun C hC â†¦ by positivityâŸ© <| h_C_lipschitz C hC.1
  Â· -- Suppose C < |f'(x)|. We need to show that f is not C-Lipschitz at x.
    have h_not_C_lipschitz : âˆ€ (C : â„â‰¥0), C < |deriv f x| â†’ Â¬LipschitzWithAt C f x := by
      intro C hC_lt
      by_contra h_contra
      obtain âŸ¨Îµ, hÎµ_pos, hÎµâŸ© : âˆƒ Îµ > 0, âˆ€ y, |y - x| < Îµ â†’ |f y - f x| â‰¤ C * |y - x| := by
        rcases Metric.mem_nhds_iff.mp h_contra with âŸ¨Îµ, Îµ_pos, hÎµâŸ©;
        use Îµ, Îµ_pos
        simp_all [edist_dist]
        intro y hy
        specialize hÎµ hy
        rw [Set.mem_setOf_eq, ENNReal.ofReal_le_iff_le_toReal] at hÎµ
        Â· simpa [ENNReal.toReal_mul, ENNReal.coe_toReal, Real.dist_eq,
            ENNReal.toReal_ofReal (abs_nonneg _)] using hÎµ
        Â· simp_all [ENNReal.mul_eq_top]
      have h_lim : (ğ“[â‰ ] x).Tendsto (fun y â†¦ |(f y - f x) / (y - x)|) (ğ“ |deriv f x|) := by
        have hd := hf.hasDerivAt
        rw [hasDerivAt_iff_tendsto_slope] at hd
        simpa [div_eq_inv_mul] using hd.abs
      have h_lim_le : âˆ€á¶  y in (ğ“[â‰ ] x), |(f y - f x) / (y - x)| â‰¤ C := by
        filter_upwards [self_mem_nhdsWithin, mem_nhdsWithin_of_mem_nhds (Metric.ball_mem_nhds x hÎµ_pos)] with y hy hy'
        rw [abs_div]
        exact div_le_of_le_mulâ‚€ (abs_nonneg _) (by positivity) (hÎµ y hy')
      exact hC_lt.not_ge <| le_of_tendsto h_lim h_lim_le;
    refine' le_of_not_gt fun h â†¦ _;
    -- By definition of infimum, if the infimum is less than |deriv f x|, there exists some C in the
    -- set such that C < |deriv f x|.
    obtain âŸ¨C, hCâ‚, hCâ‚‚âŸ© : âˆƒ C : â„â‰¥0, LipschitzWithAt C f x âˆ§ C < |deriv f x| := by
      have h_inf : âˆ€ Îµ > 0, âˆƒ C : â„â‰¥0, LipschitzWithAt C f x âˆ§ C < lipschitzAt f x + Îµ := by
        intro Îµ Îµ_pos
        exact exists_lt_of_csInf_lt (show {C : â„â‰¥0 | LipschitzWithAt C f x}.Nonempty from
          âŸ¨_, h_C_lipschitz (âŸ¨|deriv f x| + 1, by positivityâŸ© : â„â‰¥0) (by norm_num)âŸ©)
            (lt_add_of_pos_right _ Îµ_pos)
      obtain âŸ¨C, hCâŸ© := h_inf (âŸ¨|deriv f x| - lipschitzAt f x, sub_nonneg.2 h.leâŸ©) (sub_pos.2 h)
      use C, hC.1
      linarith [show C < lipschitzAt f x + (|deriv f x| - lipschitzAt f x) from mod_cast hC.2]
    tauto

theorem _root_.LipschitzWithAt_one_abs {x : â„} : LipschitzWithAt 1 abs x := by
  simp [LipschitzWithAt, edist_dist, Real.dist_eq]
  exact Filter.Eventually.of_forall (abs_abs_sub_abs_le_abs_sub Â· x)

@[simp]
theorem _root_.abs_lipschitzAt {x : â„} : lipschitzAt abs x = 1 := by
  apply le_antisymm
  Â· exact csInf_le (OrderBot.bddBelow _) LipschitzWithAt_one_abs
  Â· apply le_csInf LipschitzWithAt_one_abs.lipschitzAt
    intro b hb
    have h_lip : âˆ€á¶  z in ğ“ x, |abs z - abs x| â‰¤ b * |z - x| := by
      convert hb using 1
      simp [EDist.edist, PseudoMetricSpace.edist_dist, ENNReal.ofReal, Real.dist_eq]
      norm_cast
    contrapose! h_lip
    field_simp [Metric.eventually_nhds_iff]
    intro Îµ Îµ_pos
    rcases lt_or_ge 0 x with hx | hx
    -- Choose y = x Â± Îµ/2
    Â· use x + Îµ / 2
      norm_num [abs_of_pos, hx, Îµ_pos]
      rw [abs_of_nonneg]
      <;> cases abs_cases (x + Îµ / 2)
      <;> nlinarith [show (b : â„) < 1 from h_lip]
    Â· use x - Îµ / 2
      simp
      use by linarith [abs_of_pos Îµ_pos]
      cases abs_cases (x - Îµ / 2)
      <;> cases abs_cases x
      <;> cases abs_cases (|x - Îµ / 2| - |x|)
      <;> nlinarith [abs_of_pos (half_pos Îµ_pos), show (b : â„) < 1 from h_lip]

end lipschitzAt

/-- Map a value forward through a real function. If the real function isn't
differentiable at a point, then we get the dummy value 0 for the uncertainty. -/
@[irreducible]
noncomputable def map (f : â„ â†’ â„) (x : FOBall) : FOBall :=
  âŸ¨f x.mid,
    open Classical in
    if LipschitzAt f x.mid then (lipschitzAt f x.mid)^2 * x.var else 0âŸ©

@[simp]
theorem map_mid (f : â„ â†’ â„) (x : FOBall) : (map f x).mid = f x.mid := by
  simp [map]

@[simp]
theorem map_pure (f : â„ â†’ â„) (x : â„) : map f (pure x) = âŸ¨f x, 0âŸ© := by
  ext <;> simp [map]

theorem map_differentiableAt (f : â„ â†’ â„) (x : FOBall) (hf : DifferentiableAt â„ f x.mid) :
    map f x = âŸ¨f x.mid, âŸ¨(deriv f x.mid)^2, sq_nonneg _âŸ© * x.varâŸ© := by
  rw [map, if_pos hf.LipschitzAt, hf.lipschitzAt_eq_deriv]
  simp only [mk.injEq, mul_eq_mul_right_iff, true_and]
  left; ext; simp

@[simp]
theorem map_differentiable (f : â„ â†’ â„) (x : FOBall) (hf : Differentiable â„ f) :
    map f x = âŸ¨f x.mid, âŸ¨(deriv f x.mid)^2, sq_nonneg _âŸ© * x.varâŸ© :=
  map_differentiableAt f x hf.differentiableAt

@[simp]
theorem map_abs (x : FOBall) :
    map abs x = âŸ¨abs x.mid, x.varâŸ© := by
  simp [map, LipschitzWithAt_one_abs.lipschitzAt]

noncomputable scoped instance : FunLike (â„ â†’ â„) FOBall FOBall where
  coe f := FOBall.map f
  coe_injective' _ _ h := by
    funext x
    simpa using congrFun h x

/-- This simp lemma, arguably, makes things slightly harder to read: a simple coercion
is turned into a `map f`. But frequently enough, I expect, the implicitness of this
could confuse the reader, and make copying claims (e.g. for a `have`) harder. In this
case, it seems better to encourage explicitness of the cast. -/
@[simp]
theorem funLike_eq_map (f : â„ â†’ â„) : (â‡‘f : FOBall â†’ FOBall) = map f := by
  rfl

end map

section approx

/-- **Approximate equality of FOBalls**. See `â„Range.isApprox` for some elaboration
on what this means, what it doesn't, and why this definition was chosen. -/
def isApprox (x y : FOBall) : Prop :=
  |x.mid - y.mid| â‰¤ âˆšx.var + âˆšy.var âˆ§ y.var â‰¤ 16 * x.var âˆ§ x.var â‰¤ 16 * y.var

instance : HasEquiv FOBall :=
  âŸ¨isApproxâŸ©

/-- The provided definition of `isApprox`, which is most directly comparable to
`â„Range.isApprox`. -/
theorem isApprox_def (x y : FOBall) : x â‰ˆ y â†”
    |x.mid - y.mid| â‰¤ âˆšx.var + âˆšy.var âˆ§ y.var â‰¤ 16 * x.var âˆ§ x.var â‰¤ 16 * y.var := by
  rfl

/-- A logically equivalent definition of `isApprox` that avoids square roots, more
amenable to numerical verification. -/
@[simp]
theorem isApprox_iff (x y : FOBall) : x â‰ˆ y â†”
    (|x.mid - y.mid|^2 â‰¤ x.var + y.var âˆ¨
      (|x.mid - y.mid|^2 - x.var - y.var)^2 â‰¤ 4 * x.var * y.var) âˆ§
      (y.var â‰¤ 16 * x.var âˆ§ x.var â‰¤ 16 * y.var) := by
  rw [isApprox_def, and_congr_left_iff]
  intro _
  congr!
  constructor
  Â· intro h
    by_cases hâ‚‚ : |x.mid - y.mid| ^ 2 â‰¤ x.var + y.var
    Â· exact .inl hâ‚‚
    push_neg at hâ‚‚
    rcases x with âŸ¨xm, xvâŸ©
    rcases y with âŸ¨ym, yvâŸ©
    dsimp at h hâ‚‚ âŠ¢
    -- Since $|xm - ym| â‰¤ âˆšxv + âˆšyv, squaring both sides gives |xm - ym|^2 â‰¤ (âˆšxv + âˆšyv)^2.
    have h_sq : |xm - ym|^2 â‰¤ (âˆšxv + âˆšyv)^2 := by gcongr
    -- If $|xm - ym|^2 > xv + yv$, then $|xm - ym|^2 - xv - yv \leq 2\sqrt{xv yv}$.
    have h_diff : |xm - ym|^2 - xv - yv â‰¤ 2 * âˆš(xv * yv) := by
      rw [Real.sqrt_mul xv.zero_le_coe]
      nlinarith [Real.mul_self_sqrt xv.zero_le_coe, Real.mul_self_sqrt yv.zero_le_coe]
    -- Squaring both sides of $|xm - ym|^2 - xv - yv \leq 2\sqrt{xv yv}$, we get $(|xm - ym|^2 - xv - yv)^2 \leq 4xv yv$.
    have h_diff_sq := pow_le_pow_leftâ‚€ (by linarith) h_diff 2
    refine .inr (h_diff_sq.trans ?_)
    nlinarith only [Real.mul_self_sqrt (mul_nonneg xv.coe_nonneg yv.coe_nonneg)]
  Â· rcases x with âŸ¨xm, xvâŸ©
    rcases y with âŸ¨ym, yvâŸ©
    dsimp
    rintro (h_case1 | h_case2)
    Â· nlinarith [Real.sqrt_nonneg xv, Real.mul_self_sqrt xv.zero_le_coe,
        Real.sqrt_nonneg yv, Real.mul_self_sqrt yv.zero_le_coe]
    Â· -- Taking the square root of both sides of the inequality
      have h_sqrt_case2 : |xm - ym|^2 â‰¤ xv + yv + 2 * Real.sqrt (xv * yv ) := by
        nlinarith [Real.sqrt_nonneg (xv * yv), Real.mul_self_sqrt (by positivity : 0 â‰¤ (xv : â„) * yv)]
      rw [Real.sqrt_mul (by positivity)] at h_sqrt_case2
      nlinarith only [h_sqrt_case2, Real.sqrt_nonneg xv, Real.sqrt_nonneg yv,
        Real.mul_self_sqrt xv.coe_nonneg, Real.mul_self_sqrt yv.coe_nonneg]

instance : IsRefl FOBall (Â· â‰ˆ Â·) where
  refl x := by simp [â† NNReal.coe_le_coe]; linarith [x.var.coe_nonneg]

instance : IsSymm FOBall (Â· â‰ˆ Â·) where
  symm x y h := by
    rw [isApprox_iff] at h âŠ¢
    convert h using 1
    Â· congr! 2
      Â· rw [abs_sub_comm]
      Â· exact add_comm _ _
      Â· rw [abs_sub_comm]
        ring_nf
      Â· ring_nf
    Â· tauto

end approx
